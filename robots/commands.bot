@robot 'commands',
  local: true
  description: 'Common repl slash-commands'

cmds = {}
register = (cmd, desc = '', fn) -> cmds[cmd.toUpperCase()] = {fn, desc}

register 'help', 'Get help on commands', (args, reply) ->
  if args.length is 0
    # Reply listing the commands
    replyData = []
    for name, c of cmds
      replyData.push [name, c.desc]

    reply type:'system message', replyData
  else
    c = cmds[args[0].toUpperCase()]
    return reply 'error', {message:"unknown command #{args[0]}"} if !c

    reply type:'system message', c.desc

register 'send', 'Send a JSON message. Eg, /send chat {data:"oh hi"}', (args, reply) =>
  try
    type = args[0]
    msg = if args.length > 0 then JSON.parse args.join ' ' else {}
    @transmit msg
  catch e
    reply {type:'error', data:{stack:e.stack}}

register 'la', 'Perform an action', (args, reply) =>
  return reply 'error', 'You need to specify what action to perform' unless args.length > 0

  @transmit type:args.join ' '

register 'a', 'Perform an action', (args, reply) =>
  return reply 'error', 'You need to specify what action to perform' unless args.length > 0

  @transmit local:false, type:args.join ' '


@listen 'register command', ({data:{cmd, fn, desc}, local}) ->
  return unless local
  register cmd, desc, fn


@listen 'usercmd', ({data:cmd, local}, reply) ->
  return unless local

  args = cmd.split ' '

  command = args.shift()
  c = cmds[command.toUpperCase()]
  return reply 'error', message:"unknown command #{cmd}" if !c

  c.fn args, reply

